plugins {
	id 'java'
	id 'maven-publish'
	id "com.jfrog.bintray" version "1.1"
}

group 'ee.risk.util'
version '0.1.0'
sourceCompatibility = 1.8

repositories {
	jcenter()
}

dependencies {
	testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
}

bintray {
	user 						= project.bintrayUser
	key 						= project.bintrayKey
	publications				= ['mavenJava']
	dryRun						= false
	publish						= false

	pkg {
		repo					= 'maven'
		name					= 'util-java'
		desc					= 'Java Utilities Package'
		websiteUrl				= 'http://ivari.risk.ee/projects/utilities'
		vcsUrl					= 'https://github.com/theranger/util-java.git'
		issueTrackerUrl			= 'https://github.com/theranger/util-java/issues'
		licenses				= ['Apache-2.0']
		publicDownloadNumbers	= false
	}
}
